plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.7-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'org.springframework.cloud.contract' version '4.1.2'
	id 'maven-publish'
	id 'com.adarshr.test-logger' version '2.0.0' // test log customizer
}

group = 'io.beinspired'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
	mavenLocal()
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url "https://repo.spring.io/milestone" }
	maven { url "https://repo.spring.io/release" }
}

ext {
	set('springCloudVersion', "2023.0.1")
	set('springWebTestClient', "5.4.0")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// lombok
	implementation 'org.projectlombok:lombok'
	testImplementation 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	// Reactive Programming
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	// WEBTESTCLIENT
	testImplementation "io.rest-assured:spring-web-test-client"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('contractTest') {
	useJUnitPlatform()
	afterTest { desc, result ->
		logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
	afterTest { desc, result ->
		logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
	}
}

contracts {
//	testMode = "WEBTESTCLIENT"
	baseClassMappings {
		baseClassMapping(".*fraud.*", "io.beinspired.SCC_Producer.fraud.BaseTestClass")
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact bootJar

			artifact verifierStubsJar

			// https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/273
			versionMapping {
				usage("java-api") {
					fromResolutionOf("runtimeClasspath")
				}
				usage("java-runtime") {
					fromResolutionResult()
				}
			}
		}
	}
}


testlogger {
	theme 'standard'
}
